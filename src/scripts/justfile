set shell := ["bash", "-uc"]
name := shell("yq .name src/model/*.linkml.yml | tr '-' '_'")
title := shell("yq .title ./src/model/*.linkml.yml")
version := shell("git rev-parse --abbrev-ref HEAD")
site_title := shell("yq .title src/model/*.linkml.yml")

_default:
    @just --list --unsorted --justfile {{justfile()}}

# Cleans the output directory
_clean:
    #!/bin/bash
    set -e
    echo "Cleaning up…"
    echo
    if [ -d output ]; then \
        rm -rf output; \
    fi
    echo "… OK."
    echo

# Build and serve website locally
[group("development")]
build-and-serve: build serve

_generate_asciidoc:
    #!/bin/bash
    set -e
    echo -e "Generating documentation (AsciiDoc)…"
    mkdir -p output/docs/adoc
    cp -r src/docs/* output/docs/adoc
    yq -i '.version = "{{version}}"' output/docs/adoc/antora.yml
    yq -i '.title = "{{title}}"' output/docs/adoc/antora.yml
    echo
    echo "Generating schema documentation…"
    echo
    mkdir -p "output/docs/adoc/modules/schema"
    python -m linkml_asciidoc_generator.main \
        -o output/docs/adoc/modules/schema \
        -t /opt/dataproducten/templates \
        --render-diagrams \
        output/schemas/{{name}}.linkml.yml
    echo "Adding schema documentation to nav…"
    yq -i '.nav += ["modules/schema/nav.adoc"]' output/docs/adoc/antora.yml
    echo

_generate_additional_schemas:
    #!/bin/bash
    set -e
    echo -e "Generating artifacts…"

    cp output/schemas/{{name}}.linkml.yml output/docs/adoc/modules/schema/attachments/
    for schema in $(yq '.annotations.additional_schemas // ""' output/schemas/{{name}}.linkml.yml); do \
        if [ $schema == "json-schema" ]; then
            echo "Generating JSON Schema…"
            mkdir -p output/schemas/json-schema
            echo
            gen-json-schema \
                --not-closed \
                output/schemas/{{name}}.linkml.yml \
                > output/schemas/json-schema/{{name}}.json_schema.json
            echo "… OK."
            echo
            echo "Generated JSON Schema at: output/schemas/json-schema/{{name}}.json_schema.json"
            echo
            cp output/schemas/json-schema/{{name}}.json_schema.json output/docs/adoc/modules/schema/attachments/
        fi
        cp -r output/schemas/json-schema/{{name}}.json_schema.json output/docs/adoc/modules/schema/attachments/; \
        echo -e "To reference use:\n\txref:schema:attachment\$$schema[]"; \
    done

_generate_json_examples:
    #!/bin/bash
    set -e
    echo -e "Copy examples (JSON) to schema documentation…"
    for example in src/examples/*.yml; do \
        example_name=$(basename $example); \
        gen-linkml-profile  \
            convert \
            "$example" \
            --out "output/docs/adoc/modules/schema/attachments/${example_name%.*}.json"; \
        echo -e "To reference use:\n\txref:schema:attachment\$${example_name%.*}.json[]"; \
    done
    echo

_post-process-schema:
    mkdir -p output/schemas
    cp src/model/{{name}}.linkml.yml output/schemas/{{name}}.linkml.yml
    yq -i '.version = "{{version}}"' output/schemas/{{name}}.linkml.yml

    # Write LinkML version to schema in output.
    # Write env version as metadata to schema.
    # Generation date.
    # Do this prior to additional artifacts which could rely on it (especially the version).
    # Replace all usage of src/ to output/ in Justfile

    #@echo "Generating site using Antora…"
    #@if [ $CI == "true"  ]; then \


# Generate and assemble
[group("development"),private]
generate-and-assemble: _clean _post-process-schema validate-examples _generate_asciidoc _generate_additional_schemas _generate_json_examples

# Build website locally
[group("development")]
build: 
    @echo "Generating site using Antora…"
    @if [ $CI == "true"  ]; then \
        jinja -D site_url https://modellen.netbeheernederland.nl/{{name}} -D site_title {{site_title}} /opt/dataproducten/antora/antora-playbook.yml.jinja > /tmp/antora-playbook.yml; \
        antora --fetch /tmp/antora-playbook.yml; \
    else \
        jinja -D site_url http://localhost:8080 -D site_title {{site_title}} /opt/dataproducten/antora/antora-playbook.local.yml.jinja > /tmp/antora-playbook.local.yml; \
        antora --fetch /tmp/antora-playbook.local.yml; \
    fi
    @echo
    @echo -e "Generated documentation files at: output/docs/html"
    @echo

# Serve the built website locally
[group("development")]
serve:
    @echo -e "Serving website from output/docs/html…"
    @echo
    http-serve output/docs/html

_determine-major-version:
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_major=$(git tag -l | grep -P '^\d+\.\d+$' | cut -d . -f 1 | sort -n | tail -1)
    if [ -z "$latest_major" ]; then
        new_major=1.0;
    else
        new_major=$(echo $latest_major + 1 | bc).0;
    fi

    echo "New major version is: $new_major"
    echo
    echo "Writing new major version to LinkML schema…"
    yq -i '.version = env(version)' $OUTDIR/antora.yml

    echo "OK."
    echo


# Release new major version
[group("version-control")]
release-major-version:
    @echo TODO…

# Release new minor version
[group("version-control")]
release-minor-version:
    @echo TODO…

# Submit for review to accept changes
[group("version-control")]
submit-for-review:
    @echo TODO…

# Validate example data against schema
[group("development")]
validate-examples:
    @echo -e "Validating example data…"
    linkml-validate -s output/schemas/{{name}}.linkml.yml src/examples/*

# Create example data
[group("development")]
create-example-data:
    @echo TODO…
