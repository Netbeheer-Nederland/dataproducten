set shell := ["bash", "-uc"]
name := shell("yq .name src/model/*.linkml.yml | tr '-' '_'")
site_title := shell("yq .title src/model/*.linkml.yml")

_default:
    @just --list --unsorted --justfile {{justfile()}}


# Build
build: (_clean "output")
    yq src/model/*.linkml.yml

# Generate documentation (assumes generated artifacts [does it still?)
[group("generators")]
generate-documentation: (_clean "output")
    @echo "Generating site using Antora…"
    @if [ $CI == "true"  ]; then \
        jinja -D site_url https://modellen.netbeheernederland.nl/{{name}} -D site_title {{site_title}} /opt/dataproducten/antora/antora-playbook.yml.jinja > /tmp/antora-playbook.yml; \
        antora --fetch /tmp/antora-playbook.yml; \
    else \
        jinja -D site_url http://localhost:8080 -D site_title {{site_title}} /opt/dataproducten/antora/antora-playbook.local.yml.jinja > /tmp/antora-playbook.local.yml; \
        antora --fetch /tmp/antora-playbook.local.yml; \
    fi
    @echo
    @echo -e "Generated documentation files at: output/docs/html"
    @echo

# Clean up the provided artifact directory
[group("project")]
_clean path:
    @echo "Cleaning up…"
    @echo
    @if [ -d {{path}} ]; then \
        rm -rf {{path}}; \
    fi
    @echo "… OK."
    @echo

# Generate JSON Schema
[group("generators")]
generate-json-schema: (_clean "output/model/json-schema")
    @echo "Generating JSON Schema…"
    @echo
    gen-json-schema \
        --not-closed \
        "src/model/{{name}}.linkml.yml" \
        > "output/model/json-schema/{{name}}.json_schema.json"
    @echo "… OK."
    @echo
    @echo "Generated JSON Schema at: output/model/json-schema/{{name}}.json_schema.json"
    @echo

# Validate example data (assumes generated JSON Schema)
[group("project")]
validate-example-data:
    @echo "Validating example data against JSON schema…"
    @echo
    mkdir -p output/examples/json
    for example in src/examples/*.yml; do \
        example_json=output/examples/json/${example%.*}.json; \
        gen-linkml-profile  \
            convert \
            "$example" \
            --out $example_json; \
        check-jsonschema --schemafile "src/model/{{name}}.json_schema.json" $example_json; \
    done
    @echo "… OK."
    @echo

# Serve the generated website
[group("project")]
serve:
    http-serve output/docs/html

# Release new major version
[group("version-control")]
release-major-version: #generate-documentation validate-example-data
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_major=$(git tag -l | grep -P '^\d+\.\d+$' | cut -d . -f 1 | sort -n | tail -1)
    if [ -z "$latest_major" ]; then
        git tag 1.0;
    else
        git tag $(echo $latest_major + 1 | bc).0;
    fi

    git push --tags

    echo "… OK."
    echo

# Release new minor version
[group("version-control")]
release-minor-version:
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_version=$(git tag -l | grep -P '^\d+\.\d+$' | sort -t . -k 1,1n -k 2,2n | tail -1)
    if [ -z "$latest_version" ]; then
        git tag 1.0;
    else
        latest_major=$(echo $latest_version | cut -d . -f 1);
        latest_minor=$(echo $latest_version | cut -d . -f 2);
        new_minor=$(echo $latest_minor + 1 | bc);
        git tag $latest_major.$new_minor;
    fi

    git push --tags

    echo "… OK."
    echo

# Increment major version
[group("project")]
increment-major-version:
    #!/bin/env bash
    git fetch && git fetch --tags

    latest_major=$(git tag -l | grep -P '^\d+\.\d+$' | cut -d . -f 1 | sort -n | tail -1)
    if [ -z "$latest_major" ]; then
        new_major=1.0;
    else
        new_major=$(echo $latest_major + 1 | bc).0;
    fi

    echo "New major version is: $new_major"
    echo
    echo "Writing new major version to LinkML schema…"
    yq -i '.version = env(version)' $OUTDIR/antora.yml

    echo "OK."
    echo
