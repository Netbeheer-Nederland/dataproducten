set shell := ["bash", "-uc"]
name := shell("yq .name src/model/*.linkml.yml | tr '-' '_'")
title := shell("yq .title ./src/model/*.linkml.yml")
ref_name := "git rev-parse --abbrev-ref HEAD"
site_title := shell("yq .title src/model/*.linkml.yml")

_default:
    @just --list --unsorted --justfile {{justfile()}}

# Cleans the output directory
_clean:
    #!/bin/bash
    set -e
    echo "Cleaning up…"
    echo
    if [ -d output ]; then \
        rm -rf output; \
    fi
    echo "… OK."
    echo

# Build and serve website locally
[group("development")]
build-and-serve: build serve

_generate_asciidoc:
    #!/bin/bash
    set -e
    echo -e "Generating documentation (AsciiDoc)…"
    mkdir -p output/docs/adoc
    cp -r src/docs/* output/docs/adoc
    yq -i 'del(.version)' output/docs/adoc/antora.yml
    yq -i '.title = "{{title}}"' output/docs/adoc/antora.yml
    echo
    echo "Generating schema documentation…"
    echo
    mkdir -p "output/docs/adoc/modules/schema"
    python -m linkml_asciidoc_generator.main \
        -o output/docs/adoc/modules/schema \
        -t /opt/dataproducten/templates \
        --render-diagrams \
        output/schemas/{{name}}.linkml.yml
    echo "Adding schema documentation to nav…"
    yq -i '.nav += ["modules/schema/nav.adoc"]' output/docs/adoc/antora.yml
    echo

_generate_additional_schemas:
    #!/bin/bash
    set -e
    echo -e "Generating artifacts…"

    cp output/schemas/{{name}}.linkml.yml output/docs/adoc/modules/schema/attachments/
    for schema in $(yq '.annotations.additional_schemas // ""' output/schemas/{{name}}.linkml.yml); do \
        if [ $schema == "json-schema" ]; then
            echo "Generating JSON Schema…"
            mkdir -p output/schemas/json-schema
            echo
            gen-json-schema \
                --not-closed \
                output/schemas/{{name}}.linkml.yml \
                > output/schemas/json-schema/{{name}}.json_schema.json
            echo "… OK."
            echo
            echo "Generated JSON Schema at: output/schemas/json-schema/{{name}}.json_schema.json"
            echo
            cp output/schemas/json-schema/{{name}}.json_schema.json output/docs/adoc/modules/schema/attachments/
        fi
        cp -r output/schemas/json-schema/{{name}}.json_schema.json output/docs/adoc/modules/schema/attachments/; \
        echo -e "To reference use:\n\txref:schema:attachment\$$schema[]"; \
    done

_generate_json_examples:
    #!/bin/bash
    set -e
    echo -e "Copy examples (JSON) to schema documentation…"
    for example in src/examples/*.yml; do \
        example_name=$(basename $example); \
        gen-linkml-profile  \
            convert \
            "$example" \
            --out "output/docs/adoc/modules/schema/attachments/${example_name%.*}.json"; \
        echo -e "To reference use:\n\txref:schema:attachment\$${example_name%.*}.json[]"; \
    done
    echo

_post-process-schema:
    mkdir -p output/schemas
    cp src/model/{{name}}.linkml.yml output/schemas/{{name}}.linkml.yml
    yq -i '.version = strenv(version)' output/schemas/{{name}}.linkml.yml
    yq -i '.id = "https://modellen.netbeheernederland.nl/{{name}}/" + strenv(version)' output/schemas/{{name}}.linkml.yml

    # Write env version as metadata to schema.
    # Generation date.

# Generate and assemble
[group("development"),private]
generate-and-assemble: _clean _post-process-schema validate-examples _generate_asciidoc _generate_additional_schemas _generate_json_examples

# Build website locally
[group("development")]
build: 
    @echo "Generating site using Antora…"
    @if [ $CI == "true"  ]; then \
        jinja -D site_url https://modellen.netbeheernederland.nl/{{name}} -D site_title {{site_title}} /opt/dataproducten/antora/antora-playbook.yml.jinja > /tmp/antora-playbook.yml; \
        antora --fetch /tmp/antora-playbook.yml; \
    else \
        jinja -D site_url http://localhost:8080 -D site_title {{site_title}} /opt/dataproducten/antora/antora-playbook.local.yml.jinja > /tmp/antora-playbook.local.yml; \
        antora --fetch /tmp/antora-playbook.local.yml; \
    fi
    @echo
    @echo -e "Generated documentation files at: output/docs/html"
    @echo

# Serve the built website locally
[group("development")]
serve:
    @echo -e "Serving website from output/docs/html…"
    @echo
    http-serve output/docs/html

# Release new major version
[group("version-control")]
release-major-version:
    #!/bin/bash
    git fetch && git fetch --tags

    echo $version
    if [ {{shell(ref_name)}} != "main" ]; then
        echo 'Releasing is only allowed from the `main` branch.' && exit 1
    fi

    latest_major=$(git tag -l | grep -P '^\d+\.\d+$' | cut -d . -f 1 | sort -n | tail -1)
    if [ -z "$latest_major" ]; then
        new_version=1.0
    else
        new_version=$(echo $latest_major + 1 | bc).0
    fi

    echo "New version number is: $new_version"

    echo "Proceed with tagging the new version? [y/N]"
    read proceed
    if [ "$proceed" == "y" ]; then \
        git tag $new_version
        git push --tags
        echo "OK."
        echo "Please trigger a build and deploy action here: https://github.com/Netbeheer-Nederland/{{name}}/actions/workflows/build-and-deploy.yml"
    else
        echo "Operation canceled."
    fi
    echo

# Release new minor version
[group("version-control")]
release-minor-version:
    #!/bin/bash
    git fetch && git fetch --tags

    if [ {{shell(ref_name)}} != "main" ]; then
        echo 'Releasing is only allowed from the `main` branch.' && exit 1
    fi

    latest_version=$(git tag -l | grep -P '^\d+\.\d+$' | sort -t . -k 1,1n -k 2,2n | tail -1)
    if [ -z "$latest_version" ]; then
        new_version=1.0
    else
        latest_major=$(echo $latest_version | cut -d . -f 1)
        latest_minor=$(echo $latest_version | cut -d . -f 2)
        new_minor=$(echo $latest_minor + 1 | bc)
        new_version=$latest_major.$new_minor
    fi

    echo "New version number is: $new_version"

    echo "Proceed with tagging the new version and building? [y/N]"
    read proceed
    if [ "$proceed" == "y" ]; then \
        git tag $new_version
        git push --tags
        echo "OK."
        echo "Please trigger a build and deploy action here: https://github.com/Netbeheer-Nederland/{{name}}/actions/workflows/build-and-deploy.yml"
    else
        echo "Operation canceled."
    fi
    echo

# Submit for review to accept changes
[group("version-control")]
submit-for-review:
    @echo TODO…

# Validate example data against schema
[group("development")]
validate-examples:
    @echo -e "Validating example data…"
    linkml-validate -s output/schemas/{{name}}.linkml.yml src/examples/*

# Create example data
[group("development")]
create-example-data:
    @echo TODO…
